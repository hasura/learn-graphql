---
title: "Mutation and update cache"
metaTitle: "Apollo useMutation React hook for GraphQL mutation delete | ReScript React Apollo GraphQL Tutorial"
metaDescription: "We will use the Apollo useMutation React hook from @apollo/client with variables as an example to delete existing data and update cache locally using readQuery and writeQuery."
---

import GithubLink from "../../src/GithubLink.js";

Now let's do the integration part. Open `src/components/Todo/TodoItem.res` and add the following code to define the delete mutation

<GithubLink link="https://github.com/hasura/learn-graphql/blob/master/tutorials/frontend/rescript-react-apollo/app-final/src/components/Todo/TodoItem.res" text="src/components/Todo/TodoItem.res" />

```reason
module RemoveTodoMutation = %graphql(`
    mutation removeTodo($id: Int!) {
      delete_todos(where: { id: { _eq: $id } }) {
        affected_rows
      }
    }
  `)
```

We have a function defined to handle the button click to remove a todo. Let's update the function to use `removeTodoMutation` mutate function.

```reason
let removeTodo = e => {
    ReactEvent.Mouse.preventDefault(e)
    ReactEvent.Mouse.stopPropagation(e)
    removeTodoMutate(~update=({readQuery, writeQuery}, {data: _data}) => {
      let existingTodos = readQuery(~query=module(TodosQuery), ())
      switch existingTodos {
      | Some(todosResult) =>
        switch todosResult {
        | Ok({todos}) => {
            let newTodos = Js.Array2.filter(todos, t => t.id !== todo.id)
            let _ = writeQuery(~query=module(TodosQuery), ~data={todos: newTodos}, ())
          }
        | _ => ()
        }
      | None => ()
      }
    }, {id: todo.id})->ignore
  }
```

Let's pass optimistic response for instant UI update.

```reason
  let removeTodo = e => {
    ReactEvent.Mouse.preventDefault(e)
    ReactEvent.Mouse.stopPropagation(e)
    removeTodoMutate(
+      ~optimisticResponse=_variables => {
+        delete_todos: Some(
+          (
+            {
+              affected_rows: 1,
+              __typename: "todos_mutation_response",
+            }: RemoveTodoMutation.RemoveTodoMutation_inner.t_delete_todos
+          ),
+        ),
+      },
      ~update=({readQuery, writeQuery}, {data: _data}) => {
        let existingTodos = readQuery(~query=module(TodosQuery), ())
        switch existingTodos {
        | Some(todosResult) =>
          switch todosResult {
          | Ok({todos}) => {
              let newTodos = Js.Array2.filter(todos, t => t.id !== todo.id)
              let _ = writeQuery(~query=module(TodosQuery), ~data={todos: newTodos}, ())
            }
          | _ => ()
          }
        | None => ()
        }
      },
      {id: todo.id},
    )->ignore
  }
```
